plugins {
    id 'java'
    id 'org.openapi.generator'
    id 'java-library'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

openApiGenerate {
    generatorName = 'java'
    configOptions = [
            library: 'native',
            java8: 'true',
            dateLibrary: 'java8',
            openApiNullable: 'false']
    remoteInputSpec = project.property('usermanagement-services.api-docs.url')
    outputDir = "$buildDir/generated".toString()
    invokerPackage = "${group}.usermanagent.client"
    modelPackage = "${group}.usermanagent.client.generated.models"
    apiPackage = "${group}.usermanagent.client.generated.api"
}


ext {
    jackson_version = "2.14.1"
    jakarta_annotation_version = "1.3.5"
}

jar {
    enabled = true
}

dependencies {
    api "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    api "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    api "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    api "jakarta.annotation:jakarta.annotation-api:$jakarta_annotation_version"
    api "com.google.code.findbugs:annotations:3.0.0"
}

sourceSets.main.java.srcDir "${buildDir}/generated/src/main/java"

compileJava {
    dependsOn "openApiGenerate"
}

task copyJarToModuleNotificationProject(type: Copy) {
    from "${buildDir}/libs/user-management-client-0.0.1-SNAPSHOT.jar"
    into "${projectDir}/../service/libs"
}

// Hook the copyJarToModuleB task into the build lifecycle
build.dependsOn  copyJarToModuleNotificationProject

tasks.register("prepareKotlinBuildScriptModel"){}